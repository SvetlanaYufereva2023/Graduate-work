#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Заполнить(Команда)   
	
	ДлительнаяОперация = НачатьПоискНастроек();
		
	ПараметрыОжидания  = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект); 
	
	Оповещение = Новый ОписаниеОповещения("ПриЗавершенииПоисканастроек",ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,Оповещение,ПараметрыОжидания);
		
КонецПроцедуры   
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 &НаСервере
 Функция НачатьПоискНастроек()  
	 
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("ДатаНачала",ДатаНачала);
	ПараметрыЗапуска.Вставить("ДатаОкончания",ДатаОкончания);
	     
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
    Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
        "Обработка.ВКМ_МассовоеСозданиеАктов.МОДУЛЬОБЪЕКТА.СоздатьРеализации",
        ПараметрыЗапуска);
	 
КонецФункции	 

&НаКлиенте
Процедура ПриЗавершенииПоисканастроек(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;    
	КонецЕсли;	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
	    Возврат;
	КонецЕсли;	
	Если Результат.Статус = "Выполнено" Тогда     
		ПоказатьОповещениеПользователя(НСтр("ru = 'Успешное завершение'"),, ДополнительныеПараметры, БиблиотекаКартинок.Успешно32);
       	ОбработатьРезультат(Результат.АдресРезультата);   
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка завершена. ");
	КонецЕсли;
	
КонецПроцедуры     

 &НаСервере
 Процедура ОбработатьРезультат(АдресРезультата) 
	 
	 РезультатЗапроса = ПолучитьИзВременногоХранилища(АдресРезультата); 
	 Для каждого ЭлементМассива Из РезультатЗапроса Цикл   
		 
		 ЗаполнитьЗначенияСвойств(Объект.СписокДоговоров.Добавить(),ЭлементМассива);
		 
	 КонецЦикла;        
	 
	 УдалитьИзВременногоХранилища(АдресРезультата);   
	 
КонецПроцедуры	 
#КонецОбласти

